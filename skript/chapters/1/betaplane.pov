//
// betaplane.pov -- Darstellung des Koordinatensystems in der beta-Ebene
//
// (c) 2018 Prof Dr Andreas MÃ¼ller, Hochschule Rapperswil
//
#version 3.7;
#include "colors.inc"

global_settings {
	assumed_gamma 1
}

#declare imagescale = 0.16;

#declare arrowthickness = 0.012;

#declare breite = pi * 50 / 180;
#declare laenge = pi * 05 / 180;

#macro kugel(phi, theta)
	<cos(phi) * sin(theta), cos(theta), sin(phi) * sin(theta)>
#end

#declare B = kugel(laenge, breite);
#declare U = 0.5 * <-sin(laenge), 0, cos(laenge)>;
#declare V = 0.5 * <-cos(laenge) * cos(breite), sin(breite), -sin(laenge) * cos(breite)>;
#declare Omega = <0,1,0>;

#declare vv = 0.4 * U + 0.7 * V;
#declare C = 1 * vcross(Omega, vv);
#declare c = 4 * (vdot(C, U) * U + vdot(C, V) * V);

camera {
        location <4, 2, 2>
        look_at B + 0.2 * Omega + 0.1 * U
        right 16/9 * x * imagescale
        up y * imagescale
}

light_source {
	<10, 10, 10> color White
	area_light <1,0,0> <0,0,1>, 10, 10
	adaptive 1
	jitter
}
sky_sphere {
        pigment {
                color <1,1,1>
        }
}


#macro arrow(from, to, c)
	#declare arrowdirection = vnormalize(to - from);
	#declare arrowlength = vlength(to - from);
	union {
		sphere {
			from, 1.1 * arrowthickness
		}
		cylinder {
			from,
			from + (arrowlength - 5 * arrowthickness) * arrowdirection,
			arrowthickness
		}
		cone {
			from + (arrowlength - 5 * arrowthickness) * arrowdirection,
			2 * arrowthickness,
			to,
			0
		}
		pigment {
			color c
		}
		finish {
			specular 0.9
			metallic
		}
	}
#end

sphere {
	<0, 0, 0>, 1
	pigment {
		color rgb<0.95,0.95,0.95>
	}
	finish {
		specular 0.9
		metallic
	}
}

sphere {
	B, 2 * arrowthickness
	pigment {
		color Red
	}
	finish {
		specular 0.9
		metallic
	}
}

union {
	#declare phi = 0;
	#declare phistep = pi / 100;
	#while (phi < (2 * pi - phistep / 2))
	sphere { 
		kugel(phi, breite), arrowthickness
	}
	cylinder {
		kugel(phi, breite),
		kugel(phi + phistep, breite),
		arrowthickness
	}
	#declare phi = phi + phistep;
	#end
	pigment {
		color Red
	}
	finish {
		specular 0.9
		metallic
	}
}

union {
	sphere { <0,  1, 0>, arrowthickness }
	sphere { <0, -1, 0>, arrowthickness }
	#declare theta = 0;
	#declare thetastep = pi / 100;
	#while (theta < (pi - thetastep / 2))
		sphere { kugel(laenge, theta), arrowthickness }
		cylinder {
			kugel(laenge, theta),
			kugel(laenge, theta + thetastep),
			arrowthickness
		}
		sphere { kugel(laenge + pi, theta), arrowthickness }
		cylinder {
			kugel(laenge + pi, theta),
			kugel(laenge + pi, theta + thetastep),
			arrowthickness
		}
		#declare theta = theta + thetastep;
	#end
	pigment {
		color Red
	}
	finish {
		specular 0.9
		metallic
	}
}


arrow(B, B + U, rgb<102/255,102/255,1>)
arrow(B, B + V, rgb<51/255,153/255,1>)


mesh {
	triangle { B - U - V, B + U - V, B + U + V }
	triangle { B - U - V, B + U + V, B - U + V }
	pigment {
//		color rgbf<0.6,0.5,0.5,0.7>
		color rgbf<0.9,0.9,0.9,0.5>
	}
	finish {
		specular 0.9
		metallic
	}
}

//arrow(<-1.2,0,0>, <1.2,0,0>, White)
//arrow(<0,-1.2,0>, <0,1.2,0>, White)
//arrow(<0,0,-1.2>, <0,0,1.2>, White)

arrow(<0, 0, 0>, 1.5 * B, rgb<0,204/255,0>)

arrow(B, B + 0.5 * Omega, rgb<1,153/255,51/255>)

#declare arrowthickness = 0.7 * arrowthickness;
arrow(B, B + vv, rgb<1,0,204/255>)
//arrow(B, B + C, White)
arrow(B + vv, B + vv + C, rgb<1,204/255,204/255>)
arrow(B + vv, B + vv + c, rgb<102/255,51/255,51/255>)



